#library "sidedash"
#include "zcommon.acs"

// -----------------------------------------------------------------------------
//
// Side dashing / dodging scripts by KeksDose
//
// 23 / June / 2013
//
// This script allows you to double tap your strafing buttons to execute some
// mad side dash. Kinda like the sidestep spring in GoldenEye 007
// and Perfect Dark.
//
// Edit 20 / April / 2014:
//
// Condensed script to 80 character limit, improved some stuff. Dashing on the
// ground is more powerful, air dash adds a little jump. Using the resurrect
// cheat will no longer disable dashing. You can now dash with the turn buttons.
// And the dash sound will no longer play in the whole map. Now works like
// Unreal's dashing.
//
// Reuse and modify these scripts as you wish, just credit me somewhere.
//
// -----------------------------------------------------------------------------

// Preprocessor and global constants:

#libdefine SD_TIMEFRAME		8		// Tics during which you must double press
#libdefine SD_DASHDELAY		25		// Tics before being able to dash again
#libdefine SD_DASHSPEED		8.0	// Dash speed whilst in the air
#libdefine SD_DASHJUMP		6.0	// Adds a little z-velocity while in the air
#libdefine SD_DASHSTRONG	16.0	// And dash speed whilst grounded
#libdefine SD_SOUNDVOLUME	0.8		// How loud the dash sound is played

str dashSound = "DBLJUMP";

// Global variables:

int settings[4][2] = {
{ BT_FORWARD,				0.00 	},
{ BT_MOVELEFT | BT_LEFT,	0.25	},
{ BT_BACK,					0.50	},
{ BT_MOVERIGHT | BT_RIGHT,	0.75	}
};

function int IsButtonPressed (int input, int oldInput, int mask)
{
	//Checks For Classic Mode or CVARs
	if((GetCvar("donotsidestep") == 1)||(GetCvar("bd_classicmonsters") == 1) || (CheckInventory("GoFatality") == 1))
	{
		return false;
	}
	
	if(input & mask && !(oldinput & mask))
	{
		return true;
	}
	
	return false;
	
}

script "KD Side Dodge" enter Net
{
	int input;
	int oldInput;
	int taps;
	int tics;
	int last;
	int angle;
	int velX;
	int velY;
	int velZ;
	

	while(GetActorProperty(0, APROP_HEALTH) > 0)
	{
		input = GetPlayerInput(-1, INPUT_BUTTONS);
		oldInput = GetPlayerInput(-1, INPUT_OLDBUTTONS);
		
		for(int i = 0; i < 4; i++)
		{
			if(IsButtonPressed(input, oldInput, settings[i][0]))
			{
				taps++;
				tics = SD_TIMEFRAME;
				
				if(taps == 2 && last == i)
				{
					taps = 0;
					angle = GetActorAngle(0) + settings[i][1];
					
					if(GetActorZ(0) - GetActorFloorZ(0) > 0)
					{
						velX = FixedMul(cos(angle), SD_DASHSPEED);
						velY = FixedMul(sin(angle), SD_DASHSPEED);
						velZ = -GetActorVelZ(0) + SD_DASHJUMP;
						ACS_Execute(375, 0, 0, 0, 0);
					}
					
					else
					{
						velX = FixedMul(cos(angle), SD_DASHSTRONG);
						velY = FixedMul(sin(angle), SD_DASHSTRONG);
						velZ = 0.0;
					}
					
					SetActorVelocity(0, velX, velY, velZ, true, false);
					PlaySound(0, dashSound, CHAN_BODY, SD_SOUNDVOLUME);
					Delay(const:SD_DASHDELAY);
					
					while(GetActorVelZ(0) < 0.0)
					{
						Delay(const:1);
					}
				}
				
				last = i;
			}
		}
		
		if(tics)
		{
			tics--;
			
			if(!tics)
			{
				taps = 0;
			}
		}
		
		Delay(const:1);
	}
}

script "KD Resurrect Side Dodge" death Net
{
	while(GetActorProperty(0, APROP_HEALTH) <= 0)
		Delay(const:1);
	
	ACS_NamedExecuteAlways("KD Side Dodge", 0);
}

script "KD Reactivate Side Dodge" respawn Net
{
	ACS_NamedExecuteAlways("KD Side Dodge", 0);
}
