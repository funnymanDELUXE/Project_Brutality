/*
 * GZDoom Movement Camera Tilting
 * (inspired by the Skyrim trailer!)
 * Copyright (C) 2016 Nash Muhandes
 *
 */

// Feel free to do whatever you want with this, though credits would be
// appreciated! You don't need to ask for my permission to use this code
// in your mods.
// Nash

#library "movetilt"
#include "zcommon.acs"

// get velocity
function int GetVelocity(void)
{
	return VectorLength(GetActorVelX(0), GetActorVelY(0));
}

// Velocity multiplier
// How much the player's moving speed influences the rolling speed
#define VELOCITY_MULT 0.08

// Roll speed
// How fast the camera rolls.
// Smaller numbers reduce the camera roll speed.
#define ROLLSPEED 0.029

// Maximum angle to roll
// How far from center should the camera roll.
// Smaller numbers reduce the maximum roll amount.
#define MAXROLLANGLE 0.003

script "Script_MovementTilt" ENTER
{
	while(1)
	{
		int a, r, v;
		int cfgOld;
		int cfg = GetCVar("cl_MoveTilt");
		
		
		if (CheckInventory("PB_LockScreenTilt") > 0 )
		{
			Delay(1);
			continue;
		}

		if (cfg)
		{
			// get player's velocity
			v = FixedMul(GetVelocity(), VELOCITY_MULT);

			// increment angle
			a += ROLLSPEED;

			// clamp angle
			if (a >= 1.0 || a < 0.0)
			{
				a = 0;
			}

			// calculate roll
			r = sin(a);
			r = FixedMul(r, MAXROLLANGLE);
			r = FixedMul(r, v);

			// roll
			ChangeActorRoll(0, r, TRUE);
		}

		// detect changes in the cvar to reset the tilt
		if (cfgOld != cfg)
		{
			cfgOld = cfg;
			if (!cfg)
			{
				ChangeActorRoll(0, 0, TRUE);
			}
		}

		Delay(1);
	}
}
